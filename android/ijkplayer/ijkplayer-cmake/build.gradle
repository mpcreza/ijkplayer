apply plugin: 'com.android.library'

task cleanTempFiles(type: Delete) {
    delete fileTree("src/main/jniLibs").matching {
        include "**/*ijkplayer.so"
    }
}

android {
    afterEvaluate {
        tasks.matching {
            it.name.startsWith('ass')
        }.each { task ->
            task.dependsOn(cleanTempFiles)
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion rootProject.ext.targetSdkVersion

        externalNativeBuild {
            cmake {
                def useShell = 0 // use source
//                def useShell = 1 // use shell

                def projectPath = "${rootDir.absolutePath}"
                def ijkPath = "${projectPath}/../.."
                def ffmpegOutPath = "${new File("${projectPath}").parent}/out/libs"
                arguments '-DANDROID_STL=c++_static', "-DANDROID_TOOLCHAIN=gcc", "-DFFMPEG_OUT=$ffmpegOutPath", "-DANROID_PROJECT_PATH=$projectPath", "-DUSE_SHELL=$useShell"
            }
        }
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets.main {
        java.srcDirs = ["$rootProject.rootDir/ijkplayer-java/src/main/java/"]
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains:annotations:13.0"
}
